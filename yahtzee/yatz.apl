    ∇ SCORE←YAHTZEE;B;BOX;C;EMPTY;HEADER;REROLL;TURN;SCORES;Y
[1]    BOX←13 3⍴'1  2  3  4  5  6  3K 4K FH SS LS Y  C  '
[2]    SCORES←13⍴0
[3]   EMPTY←1+SCORES
[4]   HEADER←(⍕⍳5),9⍴'-'
[5]  PLAY:TURN←1
[6]   Y←REROLL←⍳5
[7]  NEXT:Y[REROLL]←?(⍴REROLL)⍴6
[8]   Y←Y[⍋Y]
[9]   →(TURN>2)/RECORD
[10]  ⎕←4 9⍴(9↑'TURN ',⍕TURN),HEADER,⍕Y
[11] REDO:REROLL←⎕
[12]  →(0<+/5<REROLL)/REDO ⍝ INVALID DIE SELECTION
[13]  →(1>+/REROLL)/RECORD ⍝ END TURN EARLY
[14]  TURN←TURN+1
[15]  →NEXT
[16] RECORD:⎕←3 40⍴(40↑'SCORES:'),(' ',,BOX),40↑3 0⍕SCORES
[17]  ⎕←'SCORE ',(⍕Y),' AS: (',(⍕,EMPTY⌿BOX),')'
[18]  B←⍞
[19]  →('X'=B)/0 ⍝ END GAME EARLY
[20]  B←(BOX∧.=3↑B)⍳1 ⍝ FIND SCORE BOX INDEX
[21]  →(B>13)/RECORD ⍝ INCORRECT BOX NAME
[22]  →(0=EMPTY[B])/RECORD ⍝ ALREADY SCORED
[23]  C←+⌿Y∘.=⍳6 ⍝ POPULATION COUNT
[24]  →(TOP,TK,FK,FH,SS,LS,YA,CH)[⌈/1,B-5]
[25] TOP:SCORES[B]←B×C[B]
[26]  →CONT
[27] TK:SCORES[B]←(+/Y)×2<⌈/C
[28]  →CONT
[29] FK:SCORES[B]←(+/Y)×3<⌈/C
[30]  →CONT
[31] FH:SCORES[B]←25×7>⌈/C⍳2 3
[32]  →CONT
[33] SS:SCORES[B]←30×6=⌈/((1-⍳3)⌽3 6⍴6↑4⍴1)+.=0<C
[34]  →CONT
[35] LS:SCORES[B]←40×6=⌈/((1-⍳2)⌽2 6⍴6↑5⍴1)+.=C
[36]  →CONT
[37] YA:SCORES[B]←50×1=+/4<C
[38]  →CONT
[39] CH:SCORES[B]←+/Y
[40] CONT:EMPTY[B]←0
[41]  ⎕←'SCORED ',(⍕SCORES[B]),' IN ',⍕BOX[B;]
[42]  →(1∊EMPTY)/PLAY ⍝ SCORESHEET NOT FULL
[43]  SCORE←(+/SCORES)+35×62<+/SCORES[⍳6]
[44]  ⎕←'FINAL SCORE: ',⍕SCORE
    ∇
